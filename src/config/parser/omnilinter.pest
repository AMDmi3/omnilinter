WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }

rule_title_char = {
	!("\n" | "\r" | "]") ~ ANY
	| "]]"
}
rule_title_inner = @{ rule_title_char* }
rule_title_outer = ${ "[" ~ rule_title_inner ~ "]" }
rule_title = { rule_title_outer ~ NEWLINE+ }

tag_char = {
	!("\n" | "\r" | "," | " " | "\t") ~ ANY
}
tag = @{ tag_char+ }
tags_list = { tag ~ (","? ~ tag)* }

unquoted_str_char = {
	!("\\" | "\n" | "\r" | " " | "\t" | "'" | "\"") ~ ANY
	| "\\" ~ !("\n" | "\r") ~ ANY
}
squoted_str_char = {
	!("\n" | "\r" | "'") ~ ANY
}
dquoted_str_char = {
	!("\\" | "\n" | "\r" | "\"") ~ ANY
	| "\\" ~ ("\\" | "\"")
}
unquoted_str = @{ unquoted_str_char+ }
squoted_str = @{ "'" ~ squoted_str_char* ~ "'" }
dquoted_str = @{ "\"" ~ dquoted_str_char* ~ "\"" }
concat_str = @{ (unquoted_str|squoted_str|dquoted_str)+ }
simple_glob = @{ concat_str }
included_glob = @{ !("!") ~ concat_str }
excluded_glob = @{ "!" ~ simple_glob }
globs_list_with_exclusions = { included_glob ~ included_glob* ~ excluded_glob* }

range_operation = @{ "<>" | ">=" | "<=" | ">" | "<" | "==" | "=" | "!=" }
number = @{ ASCII_DIGIT+ }
numeric_condition = { range_operation ~ number }

regexp_char = {
	!("\n" | "\r" | PEEK) ~ ANY
}
simple_regexp = @{
	PUSH(!("\n" | "\r" | " " | "\t" | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">" | "«" | "»") ~ ANY) ~
	regexp_char+ ~
	POP
}
included_regexp = @{ !("!") ~ simple_regexp }
excluded_regexp = @{ "!" ~ simple_regexp }
regexps_list_with_exclusions = { included_regexp ~ included_regexp* ~ excluded_regexp* }

rule_directive_tags = { "tags" ~ tags_list ~ NEWLINE+ }

rule_directive_files_inner = {"files" ~ globs_list_with_exclusions ~ NEWLINE+ }
rule_directive_files = {
	rule_directive_files_inner ~
	(rule_directive_match | rule_directive_nomatch | rule_directive_size | rule_directive_lines)*
}
rule_directive_nofiles = { "nofiles" ~ globs_list_with_exclusions ~ NEWLINE+ }
rule_directive_match = { "match" ~ regexps_list_with_exclusions ~ NEWLINE+ }
rule_directive_nomatch = { "nomatch" ~ regexps_list_with_exclusions ~ NEWLINE+ }
rule_directive_size = { "size" ~ numeric_condition ~ NEWLINE+ }
rule_directive_lines = { "lines" ~ numeric_condition ~ NEWLINE+ }

rule_directive = _{
	rule_directive_files |
	rule_directive_nofiles
}

rule = {
	rule_title ~
	rule_directive_tags? ~
	rule_directive*
}

config_directive_root = { "root" ~ simple_glob ~ NEWLINE+ }
config_directive_include = { "include" ~ simple_glob ~ NEWLINE+ }
config_directive = _{ config_directive_root | config_directive_include }

file = {
    SOI ~ NEWLINE* ~
	config_directive* ~
	rule* ~
    EOI
}
